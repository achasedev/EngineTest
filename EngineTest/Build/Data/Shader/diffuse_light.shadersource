
struct VInput
{
	float3 position : POSITION;
	float4 color : COLOR;
	float2 uv : UV;
  float3 normal : NORMAL;
  float4 tangent : TANGENT;
};

struct VOut
{
	  float4 position : SV_POSITION;
    float4 color : COLOR;
    float2 uv : UV;
    float3 worldPosition : POSITIONT; // For determining light direction
    float4x4 TBNTransform : NORMAL;          
};

Texture2D<float4> tAlbedo : register(t0);
SamplerState sAlbedo : register(s0);

Texture2D<float4> tNormal : register(t1);
SamplerState sNormal : register(s1);

cbuffer cameraConstants : register(b2)
{
   float4x4 CAMERA;
   float4x4 VIEW; 
   float4x4 PROJECTION; 
};

cbuffer modelConstant : register(b3)
{
   float4x4 MODEL; 
};

VOut VertexFunction(VInput input)
{
    float4 localPos = float4(input.position, 1.0f);
    float4 worldPos = mul(MODEL, localPos);
    float4 viewPos = mul(VIEW, worldPos); 
    float4 clipPos = mul(PROJECTION, viewPos); 

    VOut output;

    output.position = clipPos;
    output.color = input.color;
    output.uv = input.uv;
    output.worldPosition = worldPos.xyz;

    float3 worldNormal = normalize(mul(MODEL, float4(input.normal, 0.f)).xyz);
    float3 worldTangent = normalize(mul(MODEL , float4(input.tangent.xyz, 0.f)).xyz);
    float3 worldBitangent = cross(worldTangent, worldNormal) * input.tangent.w;
    output.TBNTransform = float4x4(float4(worldTangent, 0.f), float4(worldBitangent, 0.f), float4(worldNormal, 0.f), float4(worldPos.xyz, 1.0f));
    output.TBNTransform = transpose(output.TBNTransform);
    
    return output;
}

struct Light
{
  float3    m_position;
  float     m_dotOuterAngle;
  float3    m_direction;
  float     m_dotInnerAngle;
  float3    m_attenuationFactors;
  float     m_directionFactor;
  float4    m_color;
  float4x4  m_shadowVP;
  float3    m_padding;
  float     m_castsShadows;
};

#define MAX_LIGHTS 8
cbuffer lightContant : register(b4)
{
    float4  AMBIENT; // xyz color, w intensity
    Light   LIGHTS[MAX_LIGHTS];
};


float3 CalculateWorldNormal(float4 color, float4x4 tbnTransform)
{
  // Range map the values (Z should be between 0.5 and 1.0 always
  float3 surfaceNormal = normalize((2.f * color.xyz) - float3(1.f, 1.f, 1.f));
  return normalize(mul(tbnTransform, float4(surfaceNormal, 0.f)).xyz);
}

float CalculateAttenuation(float3 lightPosition, float3 worldPosition, float3 attenuationFactors, float intensity)
{
    float d = length(lightPosition - worldPosition);
    float denominator = attenuationFactors.x + attenuationFactors.y * d + attenuationFactors.z * d * d;
    return (intensity / denominator);
}   

// Calculates the cone factor (= 1.f for directional and point lights, 0.f <= x <= 1.f for spot lights)
float CalculateConeFactor(float3 lightPosition, float3 worldPosition, float3 lightDirection, float outerDotThreshold, float innerDotThreshold)
{
    float dotFactor = dot(normalize(worldPosition - lightPosition), lightDirection);
    float coneFactor = smoothstep(outerDotThreshold, innerDotThreshold, dotFactor);

    return coneFactor;
}     

float3 CalculateDot3(float3 directionToLight, float3 normal, float4 lightColor, float attenuation, float coneFactor)
{
    return (max(0.f, dot(directionToLight, normal)) * lightColor.xyz * lightColor.w * attenuation * coneFactor);
}

float4 FragmentFunction( VOut input ) : SV_Target0
{    
    float4 surfaceColor = tAlbedo.Sample(sAlbedo, input.uv);
    float4 normalColor = tNormal.Sample(sNormal, input.uv);
    float3 worldNormal = CalculateWorldNormal(normalColor, input.TBNTransform);
    float3 surfaceLight = float3(0.f, 0.f, 0.f); // No ambient
  
    for (int lightIndex = 0; lightIndex < MAX_LIGHTS; ++lightIndex)
    {
        float3 directionToLight = lerp(-LIGHTS[lightIndex].m_direction, normalize(LIGHTS[lightIndex].m_position - input.worldPosition), LIGHTS[lightIndex].m_directionFactor);
        float attenuation = CalculateAttenuation(LIGHTS[lightIndex].m_position, input.worldPosition, LIGHTS[lightIndex].m_attenuationFactors, LIGHTS[lightIndex].m_color.w);
        float coneFactor = CalculateConeFactor(LIGHTS[lightIndex].m_position, input.worldPosition, LIGHTS[lightIndex].m_direction, LIGHTS[lightIndex].m_dotOuterAngle, LIGHTS[lightIndex].m_dotInnerAngle);
        surfaceLight += CalculateDot3(directionToLight, worldNormal, LIGHTS[lightIndex].m_color, attenuation, coneFactor);
    }
  
    surfaceLight = clamp(surfaceLight, float3(0.f, 0.f, 0.f), float3(1.f, 1.f, 1.f));
  
    float4 finalColor = float4(surfaceLight, 1.f) * surfaceColor * input.color;
    finalColor = clamp(finalColor, float4(0.f, 0.f, 0.f, 0.f), float4(1.f, 1.f, 1.f, 1.f));
    
    return finalColor;
}
