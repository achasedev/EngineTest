struct VInput
{
	float3 position : POSITION;
	float4 color : COLOR;
	float2 uv : UV;
    float3 normal : NORMAL;
    float4 tangent : TANGENT;
};

struct VOut
{
	float4      position : SV_POSITION;
    float4      color : COLOR;
    float2      uv : UV;
    float3      worldPosition : POSITIONT; // For determining light direction
    float4x4    TBNTransform : NORMAL;          
    float3      eyePosition : POSITION;
};

Texture2D<float4> tNormal : register(t1);
SamplerState sNormal : register(s1);

cbuffer cbCamera : register(b1)
{
   float4x4 CAMERA;
   float4x4 VIEW; 
   float4x4 PROJECTION; 
};

cbuffer cbModel : register(b2)
{
   float4x4 MODEL; 
};

struct Light
{
    float3    m_position;
    float     m_dotOuterAngle;
    float3    m_direction;
    float     m_dotInnerAngle;
    float3    m_attenuationFactors;
    float     m_directionFactor;
    float4    m_color;
    float4x4  m_shadowVP;
    float3    m_padding;
    float     m_castsShadows;
};

#define MAX_LIGHTS 8
cbuffer cbLight : register(b3)
{
    float4  AMBIENT; // xyz color, w intensity
    Light   LIGHTS[MAX_LIGHTS];
};

cbuffer cbSpecular : register(b8)
{
    float SPECULAR_AMOUNT;
    float SPECULAR_POWER;
    float2 PADDING0;
}

VOut VertexFunction(VInput input)
{
    float4 localPos = float4(input.position, 1.0f);
    float4 worldPos = mul(MODEL, localPos);
    float4 viewPos = mul(VIEW, worldPos); 
    float4 clipPos = mul(PROJECTION, viewPos); 

    VOut output;

    output.position = clipPos;
    output.color = input.color;
    output.uv = input.uv;
    output.worldPosition = worldPos.xyz;
    output.eyePosition = CAMERA._m03_m13_m23;

    float3 worldNormal = normalize(mul(MODEL, float4(input.normal, 0.f)).xyz);
    float3 worldTangent = normalize(mul(MODEL , float4(input.tangent.xyz, 0.f)).xyz);
    float3 worldBitangent = cross(worldTangent, worldNormal) * input.tangent.w;
    output.TBNTransform = float4x4(float4(worldTangent, 0.f), float4(worldBitangent, 0.f), float4(worldNormal, 0.f), float4(worldPos.xyz, 1.0f));
    output.TBNTransform = transpose(output.TBNTransform);
    
    return output;
}

float3 CalculateWorldNormal(float4 color, float4x4 tbnTransform)
{
  // Range map the values (Z should be between 0.5 and 1.0 always
  float3 surfaceNormal = normalize((2.f * color.xyz) - float3(1.f, 1.f, 1.f));
  return normalize(mul(tbnTransform, float4(surfaceNormal, 0.f)).xyz);
}

float CalculateAttenuation(float3 lightPosition, float3 worldPosition, float3 attenuationFactors, float intensity)
{
    float d = length(lightPosition - worldPosition);
    float denominator = attenuationFactors.x + attenuationFactors.y * d + attenuationFactors.z * d * d;
    return (intensity / denominator);
}   


float3 CalculateSpecular(float3 directionToLight, float3 normal, float3 directionToEye, float4 lightColor, float attenuation, float coneFactor)
{
  float3 reflectDir = reflect(-directionToLight, normal);
  float factor = max(0, dot(directionToEye, reflectDir));
  float3 specular = SPECULAR_AMOUNT * pow(factor, SPECULAR_POWER) * lightColor.xyz * lightColor.w * attenuation * coneFactor;
  
  return specular;
}

float4 FragmentFunction( VOut input ) : SV_Target0
{    
    float3 directionToEye = normalize(input.eyePosition - input.worldPosition);
  
    // Get the normal from the normal map, and transform it into TBN space
    float4 normalColor = tNormal.Sample(sNormal, input.uv);
    float3 worldNormal = CalculateWorldNormal(normalColor, input.TBNTransform);

    // Calculate the direction TO light, attenuation, and cone factor for each light
    // Set up accumulation variables
    float3 reflectedLight = float3(0.f, 0.f, 0.f);  // How much light is being reflected back
  
    for (int lightIndex = 0; lightIndex < MAX_LIGHTS; ++lightIndex)
    {
        // Directions to the light
        float3 directionToLight = lerp(-LIGHTS[lightIndex].m_direction, normalize(LIGHTS[lightIndex].m_position - input.worldPosition), LIGHTS[lightIndex].m_directionFactor);
        float attenuation = CalculateAttenuation(LIGHTS[lightIndex].m_position, input.worldPosition, LIGHTS[lightIndex].m_attenuationFactors, LIGHTS[lightIndex].m_color.w);

        reflectedLight += CalculateSpecular(directionToLight, worldNormal, directionToEye, LIGHTS[lightIndex].m_color, attenuation, 1.0f);
    }
  
    float4 finalColor = float4(reflectedLight, 1.f);
    finalColor = clamp(finalColor, float4(0.f, 0.f, 0.f, 0.f), float4(1.f, 1.f, 1.f, 1.f));
    
    return finalColor;
}
